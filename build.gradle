plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id "me.champeau.jmh" version "0.7.3"
}

group = 'io.github.alexwibowo'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.assertj:assertj-core:3.11.1"

}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

jmh {
    warmupIterations = 5
    iterations = 5
    fork = 1
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
    tagTemplate = '${version}'
    versionPropertyFile = 'gradle.properties'
    snapshotSuffix = '-SNAPSHOT'
    buildTasks = []

    git {
        requireBranch.set('main')
    }
}

publishing {
    publications{

        mavenJava(MavenPublication){
//            groupId = ${project.group}
//            artifactId = ${project.name}
//            version = ${project.version}
            from components.java

            pom {
//                name = '${project.name}'
                url = 'https://github.com/alexwibowo/chronotools'
                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'alexwibowo'
                        name = 'Alex Wibowo'
                        email = 'alexwibowo@gmail.com'
                    }
                }
                scm {
                    connection='scm:git:git:github.com/alexwibowo/chronotools.git'
                    developerConnection='scm:git:https://github.com/alexwibowo/chronotools.git'
                    url='https://github.com/alexwibowo/chronotools'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("ossrhUsername") ?: ""
                password = project.findProperty("ossrhPassword") ?: ""
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}