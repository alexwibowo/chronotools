plugins {
    id 'java'
    id 'net.researchgate.release' version '3.1.0'
    id "me.champeau.jmh" version "0.7.3"
    id "com.vanniktech.maven.publish" version "0.31.0"
}

group = 'io.github.alexwibowo'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.assertj:assertj-core:3.11.1"

}

test {
    useJUnitPlatform()
}

jmh {
    warmupIterations = 5
    iterations = 5
    fork = 1
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
    tagTemplate = '${version}'
    versionPropertyFile = 'gradle.properties'
    snapshotSuffix = '-SNAPSHOT'
    buildTasks = []

    git {
        requireBranch.set('main')
    }
}


mavenPublishing {
    coordinates("io.github.alexwibowo", "chronotools", "1.3")

    pom {
        name = "Chronotools"
        description = "Java library for fast date and time calculations."
        inceptionYear = "2025"
        url = "https://github.com/alexwibowo/chronotools/"
        licenses {
            license {
                name = "MIT License"
                url = "http://opensource.org/licenses/MIT"
                distribution = "http://opensource.org/licenses/MIT"
            }
        }
        developers {
            developer {
                id = "AlexWibowo"
                name = "Alex Wibowo"
                url = "https://github.com/alexwibowo/"
            }
        }
        scm {
            url = "https://github.com/alexwibowo/chronotools/"
            connection = "scm:git:git://github.com/alexwibowo/chronotools.git"
            developerConnection = "scm:git:ssh://git@github.com/alexwibowo/chronotools.git"
        }
    }
}